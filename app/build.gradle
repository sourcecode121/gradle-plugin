apply plugin: 'com.android.application'

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.2"

    defaultConfig {
        applicationId "com.example.gradleplugin"
        minSdkVersion 15
        targetSdkVersion 24
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:24.2.1'
}

apply plugin: ChangeReadMeVersionPlugin

class ChangeReadMeVersionPlugin implements Plugin<Project> {

    @Override
    void apply(Project project){
        project.afterEvaluate {
            project.task('changeVersion', type: ChangeVersion){
                readMe = new File(project.rootDir.absolutePath + "/README.md")
                versionName = project.android.defaultConfig.versionName
            }
        }
    }
}

class ChangeVersion extends DefaultTask {
    def readMe
    def versionName

    ChangeVersion(){
        group = 'myGradlePlugin'
    }

    File getReadMe(){
        project.file(readMe)
    }

    @TaskAction
    def change(){
        description = 'Update version info in the README file'

        File readMeFile = getReadMe()
        String contents = readMeFile.getText('UTF-8')
        contents = contents.replaceAll("myGradlePlugin:.*", "myGradlePlugin:${versionName}")
        readMeFile.write(contents, 'UTF-8')
    }
}
